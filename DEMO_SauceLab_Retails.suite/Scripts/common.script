to textField label, data, config
	if there is a property offset of config
		set offset to config.offset
	else
		set offset to [0,0]
	end if
	
	Click ImageLocation(text:label, caseSensitive:true) + offset
	wait 0.5
	TypeText data
	wait 0.5
	Click ImageLocation(text:label, caseSensitive:true)
	wait 1
end textField

to passwordField label, data, config
	if there is a property offset of config
		set offset to config.offset
	else
		set offset to [0,0]
	end if
	
	Click ImageLocation(text:label, caseSensitive:true) + offset
	wait 0.5
	TypeHiddenText data
	wait 0.5
	Click ImageLocation(text:label, caseSensitive:true)
	wait 0.5
end passwordField


to navigate navigate, validate, configNavigate, configValidate

	if configNavigate is not empty then common.setStyle(configNavigate)
	if (SuiteInfo().imagesFolder & "/" & navigate) is a folder or (SuiteInfo().imagesFolder & "/" & navigate & ".png") is a file
		Click {image:navigate}
	else
		Click {text:navigate, caseSensitive:true}
	end if
	
	set the textstyle to "Default"
	
	if validate is not empty and validate is not null
		if configValidate is not empty then common.setStyle(configValidate)
		
		if (SuiteInfo().imagesFolder & "/" & validate) is a folder or (SuiteInfo().imagesFolder & "/" & validate & ".png") is a file
			waitFor 60, image:validate
		else
			waitFor 60, text:validate, caseSensitive:true
		end if
		set the textstyle to "Default"
	end if
	
end navigate

to navigateDown navigate, validate, configNavigate, configValidate

	if configNavigate is not empty then common.setStyle(configNavigate)
	if (SuiteInfo().imagesFolder & "/" & navigate) is a folder or (SuiteInfo().imagesFolder & "/" & navigate & ".png") is a file
		Repeat until ImageFound(image:navigate,waitFor: 2)
			swipeUp
			wait 0.5
		End repeat
		wait 1
		Click image:navigate
	else
		Repeat until ImageFound(text:navigate,waitFor: 2, caseSensitive:true)
			swipeUp
			wait 0.5
		End repeat
		wait 1
		Click {text:navigate, caseSensitive:true}
	end if
	set the textstyle to "Default"
	
	
	if validate is not empty
		if configValidate is not empty then common.setStyle(configValidate)
		
		if (SuiteInfo().imagesFolder & "/" & validate) is a folder or (SuiteInfo().imagesFolder & "/" & validate & ".png") is a file
			waitFor 60, image:validate
		else
			waitFor 60, text:validate, caseSensitive:true
		end if
		set the textstyle to "Default"
	end if
end navigateDown

to setStyle config
	-- Idealy we would jsut want to loop through the list and add any attributes to the textStyle
	if there is a property parameters of config and there is a property searchRectangle of config then set the textstyle to config.parameters
	If there is a property parameters of config and there is a property searchRectangle of config then
		set the textstyle to config.parameters &&& config.searchRectangle
	Else if (there is a property parameters of config and there is not a property searchRectangle of config)
		set the textstyle to config.parameters
	Else if (there is not a property parameters of config and there is a property searchRectangle of config)
		set the textstyle to config.searchRectangle
	Else
		Log "Style not set as there is no property in the config"
	End if
end setStyle